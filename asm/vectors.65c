.INCLUDE "asm/common.inc"
.INCLUDE "asm/strout.inc"

.ORGA $F7FA
.BANK 0 SLOT 0
.SECTION "!InterruptVectors" FORCE
.DW handleNMI, handleReset, handleIRQ
.ENDS

.EXPORT g_NMICount

.ORG $0
.RAMSECTION "x" SLOT ZeroPageSlot
g_HilightStart DB
g_HilightStop DB
g_tmp DB
.ENDS

; WLA doesn't give us a way to put particular things into RAMSECTIONs at
; aligned or otherwise hardcoded addresses...
.DEFINE g_Framebuffer $4000
.EXPORT g_Framebuffer

.ORG $0
.BANK 0 SLOT 0
.SECTION "Startup" FREE
handleReset:
	; If SimpleConfig is embedded in another cartridge, this work will be
	; done before main is called. If it is standalone, we can do some
	; basic initialization ourselves.

	; We can have pretty simple startup code because we don't use the BG or
	; SP layers.

	; Video is supposed to be blank on reset, but make sure anyway
	STZ r_Multi

	; All-zero palette locations for BG and SP, and BGFG
	LDX #9
-	DEX
	STZ r_SPPalettes,X
	BNE -

	; Zero color mod
	STZ r_ColorMod

	; Black out most of the CRAM, we live in the top 32 colors
	STZ r_CRAMIndex
	LDA #$FF
	LDX #256-32
-	STA r_CRAMPort
	DEX
	BNE -

	; While we're at it, stub out all the sprites
	STZ r_SSMIndex
	LDX #64
-	STZ r_SSMPort
	STA r_SSMPort
	STZ r_SSMPort
	STZ r_SSMPort
	DEX
	BNE -

	; "Purple out" the next 16 colors (for the hilight background)
	LDA #$1A
	LDX #16
-	STA r_CRAMPort
	DEX
	BNE -

	; The overlay palette
	LDA #$30
	LDY #$20
	JSR _loadPalSeg
	LDA #$4A
	JSR _loadPalSeg
	LDA #$50
	LDY #$30
	JSR _loadPalSeg
	JSR _loadPalSeg

	; Go!
	JSR main
	STP

_loadPalSeg:
	STY r_CRAMPort
	STA r_CRAMPort
	STY r_CRAMPort
	STA r_CRAMPort
	RTS

.ENDS

.ORG $0
.SECTION "Interrupts" FREE
handleNMI:
	PHA
	LDA #8
	STA r_IRQScanline
	; if we're embedded in another cartridge, which includes the ET license
	; block, this is redundant but harmless
	INC g_NMICount
	PLA
	RTI

_BRKmessage:
.DB "BRK encountered", 10, 0
handleBRK:
	callStrout _BRKmessage
	STP

handleIRQ:
	; be conservative, to allow us to be called either as a raw IRQ handler
	; or by another IRQ handler that has already checked for BRK
	PHX
	TSX
	PHA
	INX
	INX
	LDA #$10
	BIT $0100,X
	BNE handleBRK
	; it's not a BRK; set the registers appropriately
	LDA r_IRQScanline
	SEC
	SBC #8
	; Set the overlay base nybble appropriately (do this first)
	PHA ; (back up A)
	LSR
	LSR
	AND #$30
	ORA #$48
	STA r_Multi
	; Are we within the hilight region?
	PLA ; (restore backed up A)
	CMP g_HilightStart
	BMI +
	CMP g_HilightStop
	BPL +
	; We are within the hilight region
	LDA #31
	STA r_OLPalette
	LDA #$0E
	BRA ++
+	; We are not within the hilight region
	LDA #30
	STA r_OLPalette
	LDA #0
++	STA r_BGPalettes
	STA r_BGPalettes+1
	STA r_BGPalettes+2
	STA r_BGPalettes+3
+++	; This was fun, let's do another IRQ in 8 more scanlines
	LDA r_IRQScanline
	CLC
	ADC #8
	STA r_IRQScanline
	PLA
	PLX
	RTI

.ENDS
